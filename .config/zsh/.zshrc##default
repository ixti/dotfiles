# vim: ft=zsh

if [[ ! -f "${ZDOTDIR}/.zshrc.zwc" || "${ZDOTDIR}/.zshrc" -nt "${ZDOTDIR}/.zshrc.zwc" ]]; then
  # Recompile in background
  (zcompile -- "${ZDOTDIR}/.zshrc.zwc" "${ZDOTDIR}/.zshrc") &!
fi

# Uncomment to profile
# zmodload zsh/zprof

# ZSH Options -- https://zsh.sourceforge.io/Doc/Release/Options.html
################################################################################

# Core interaction options
setopt autocd              # Change directories just by typing their names.
setopt autopushd           # Keep a directory stack when using cd.
setopt pushdignoredups     # Avoid duplicates in directory stack.
setopt nobeep              # Disable annoying terminal beep.
setopt interactivecomments # Allow comments in interactive mode.
setopt correct             # Try to correct the spelling of commands.
setopt nocorrectall        # But don't correct arguments.

# Auto-completion
setopt completeinword      # Complete from both ends of a word.
setopt alwaystoend         # Move cursor to the end of a completed word.
setopt pathdirs            # Perform path search even on command names with slashes.
setopt automenu            # Show completion menu on a successive tab press.
setopt autolist            # Automatically list choices on ambiguous completion.
setopt autoparamslash      # If completed parameter is a directory, add a trailing slash.
unsetopt menucomplete      # Do not autoselect the first completion entry.
unsetopt flowcontrol       # Disable start/stop characters in shell editor.

# Filename and globbing behavior
setopt extendedglob        # Enable advanced globbing (e.g., ^, ~, and ** wildcards).
setopt nocaseglob          # Make filename completion case-insensitive.
setopt nomatch             # Print an error when glob patterns don't match.
setopt noglobdots          # Hide dotfiles unless you explicitly include them.

# Command history
setopt extendedhistory     # Record timestamps in history.
setopt histignoredups      # Donâ€™t record duplicate commands.
setopt histignorespace     # Ignore commands prefixed with a space.
setopt histreduceblanks    # Remove redundant empty lines from history.
setopt incappendhistory    # Append to history file incrementally (no loss on crash).
setopt sharehistory        # Share history across multiple Zsh sessions.

# Safety and strictness
setopt noclobber           # Prevent overwriting files with > redirection.
setopt localoptions        # Make option changes local to functions.
setopt localtraps          # Make traps local to functions.

# Usability enhancements
setopt promptsubst         # Allow variable expansion in prompts.
setopt nohup               # Prevent background jobs from hanging up on logout.
setopt notify              # Report job status changes immediately.

# ENV meaningful in the interactive shell only
################################################################################

# A themeable LS_COLORS generator with a rich filetype datebase.
# https://github.com/sharkdp/vivid/
if (( ${+commands[vivid]} )); then
  typeset -gx LS_COLORS="$(vivid generate gruvbox-dark-hard)"
else
  typeset -gx LS_COLORS="${LS_COLORS:-'di=34:ln=35:so=32:pi=33:ex=31:bd=36;01:cd=33;01:su=31;40;07:sg=36;40;07:tw=32;40;07:ow=33;40;07:'}"
fi

# ENV used by plugins
################################################################################

typeset -rgxi ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
typeset -rgxa ZSH_AUTOSUGGEST_STRATEGY=(history completion)
typeset -rgx  ZSH_COMPDUMP="${ZCACHEDIR}/zcompdump"

# Plugins
################################################################################

zstyle ':antidote:*'            'zcompile'   'yes'
zstyle ':plugin:ez-compinit'    'compstyle'  'prez'
zstyle ':plugin:ez-compinit'    'use-cache'  'yes'

source "${ZDOTDIR}/antidote/antidote.zsh"
antidote load

# Integrations
################################################################################

# The front-end to your dev env
# https://mise.jdx.dev/
if (( ${+commands[mise]} )) && (( ! ${+functions[mise]} )); then
  zsh-defer -c 'source <(command mise activate zsh)'
fi

# The minimal, blazing-fast, and infinitely customizable prompt.
# https://starship.rs/
if (( ${+commands[starship]} )); then
  # See: https://github.com/starship/starship/issues/2637
  # source <(command starship init zsh --print-full-init)
  source <(command starship init zsh)
fi

# A command-line fuzzy finder
# https://github.com/junegunn/fzf
if (( ${+commands[fzf]} )); then
  source <(command fzf --zsh)
fi

# A smarter cd command.
# NOTE: Must be last thing in the config, if used with `--cmd cd`
# https://github.com/ajeetdsouza/zoxide
if (( ${+commands[zoxide]} )); then
  source <(command zoxide init zsh)
fi

# Functions
################################################################################

autoload -Uz gh-access-token
autoload -Uz zz

# Location Aliases
################################################################################

hash -d guru-overlay=/var/db/repos/guru
hash -d gentoo-overlay=/var/db/repos/gentoo

# Command Aliases
################################################################################

# Make standard ls colourful automagically, but don't replace it with LSD
alias ls='ls --color=auto'

# The next gen ls command
# https://github.com/lsd-rs/lsd
if (( ${+commands[lsd]} )); then
  alias ll='lsd --all -l'
  alias la='lsd --almost-all'
else
  alias ll='ls --color=auto --all --long'
  alias la='ls --color=auto --almost-all'
fi

# Git
alias gbv="git branch --verbose --verbose"
alias gfp="git fetch --prune"
alias gri="git rebase --interactive"

# Ruby
alias be="bundle exec"
alias bi="bundle install"
alias bu="bundle update"
alias bo="bundle open"
alias ror="bundle exec rails"

# Other tools
alias om="overmind"
alias av="aws-vault"
alias ave="aws-vault exec"

# Local Overrides
################################################################################

local zshrc_local="${ZDOTDIR}/local/.zshrc"
[[ -r "$zshrc_local" ]] && source "$zshrc_local"
unset zshrc_local

################################################################################

(( ${+builtins[zprof]} )) && zprof || true
