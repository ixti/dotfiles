#!/usr/bin/env zsh
# Vivid filetypes & themes auto-updater with interactive yadm commit

set -euo pipefail

xdg_config_dir="${XDG_CONFIG_HOME:-"${HOME}/.config"}"
vivid_config_dir="${vivid_CONFIG_DIR:-"${xdg_config_dir}/vivid"}"
vivid_themes_dir="${vivid_config_dir}/themes"

[[ -d "${vivid_themes_dir}" ]] || mkdir -p "${vivid_themes_dir}"

typeset -a updated_files=()

sync() {
  print -u2 "* ${1}\n  â†ª ${2}"

  if curl --fail -slo "${2}" "${1}"; then
    if (( ${+commands[yadm]} )) && [[ -n "$(yadm status --porcelain "${2}")" ]]; then
      updated_files+=("${2}")
    fi
  fi
}

update_vivid_filetypes() {
  local src="https://raw.githubusercontent.com/sharkdp/vivid/refs/heads/master/config/filetypes.yml"
  local dst="${vivid_config_dir}/$(basename "$src")"

  sync "${src}" "${dst}"
}

update_vivid_theme() {
  local src="$1"
  local dst="${vivid_themes_dir}/${2:-"$(basename "$src")"}"

  sync "${src}" "${dst}"
}

update_vivid_filetypes

update_vivid_theme "https://raw.githubusercontent.com/sharkdp/vivid/refs/heads/master/themes/tokyonight-day.yml"
update_vivid_theme "https://raw.githubusercontent.com/sharkdp/vivid/refs/heads/master/themes/tokyonight-storm.yml"
update_vivid_theme "https://raw.githubusercontent.com/sharkdp/vivid/refs/heads/master/themes/tokyonight-moon.yml"
update_vivid_theme "https://raw.githubusercontent.com/sharkdp/vivid/refs/heads/master/themes/tokyonight-night.yml"
update_vivid_theme "https://raw.githubusercontent.com/scottmckendry/cyberdream.nvim/refs/heads/main/extras/vivid/cyberdream.yml"
update_vivid_theme "https://raw.githubusercontent.com/scottmckendry/cyberdream.nvim/refs/heads/main/extras/vivid/cyberdream-light.yml"

if (( ${+commands[yadm]} && ${#updated_files[@]} )); then
  yadm add "${updated_files[@]}"

  print -u2 "\nDo you want to commit the following updated Vivid filetypes/themes to yadm?"

  for f in "${updated_files[@]}"; do
    print -u2 "  ${f}"
  done

  print -u2 -n "> Commit these files? (y/N): "
  read -r reply

  if [[ "$reply" =~ ^[Yy] ]]; then
    yadm commit "${updated_files[@]}" -m "chore(vivd): Update filetypes & themes"
  else
    print -u2 "Skipped yadm commit."
  fi
fi

unset xdg_config_dir vivid_config_dir vivid_themes_dir
unset updated_files sync update_vivid_filetypes update_vivid_theme
