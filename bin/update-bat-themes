#!/usr/bin/env zsh
# Bat theme auto-updater with interactive yadm commit

set -euo pipefail

NIGHTFOX_VARIANTS=(carbonfox dawnfox dayfox duskfox nightfox nordfox terafox)
TOKYONIGHT_VARIANTS=(day storm moon night)

_xdg_config_dir="${XDG_CONFIG_HOME:-"${HOME}/.config"}"
_bat_config_dir="${BAT_CONFIG_DIR:-"${_xdg_config_dir}/bat"}"

BAT_THEMES_DIR="${_bat_config_dir}/themes"
unset _xdg_config_dir _bat_config_dir

[[ -d "${BAT_THEMES_DIR}" ]] || mkdir -p "${BAT_THEMES_DIR}"

typeset -a UPDATED_FILES=()

sync() {
  print -u2 "* ${1}\n  â†ª ${2}"

  if curl --fail -slo "${2}" "${1}"; then
    if (( ${+commands[yadm]} )) && [[ -n "$(yadm status --porcelain -uall "${2}")" ]]; then
      UPDATED_FILES+=("${2}")
    fi
  fi
}

update_bat_theme() {
  local src="$1"
  local dst="${BAT_THEMES_DIR}/${2:-"$(basename "$src")"}"

  sync "${src}" "${dst}"
}

update_bat_theme "https://raw.githubusercontent.com/scottmckendry/cyberdream.nvim/refs/heads/main/extras/textmate/cyberdream.tmTheme"
update_bat_theme "https://raw.githubusercontent.com/scottmckendry/cyberdream.nvim/refs/heads/main/extras/textmate/cyberdream-light.tmTheme"

for variant in "${NIGHTFOX_VARIANTS[@]}"; do
  update_bat_theme "https://raw.githubusercontent.com/EdenEast/nightfox.nvim/refs/heads/main/extra/${variant}/${variant}.tmTheme"
done
unset variant

for variant in "${TOKYONIGHT_VARIANTS[@]}"; do
  update_bat_theme \
    "https://raw.githubusercontent.com/folke/tokyonight.nvim/refs/heads/main/extras/sublime/tokyonight_${variant}.tmTheme" \
    "tokyonight-${variant}.tmTheme"
done
unset variant

if (( ${+commands[bat]} )); then
  bat cache --build
fi

if (( ${+commands[yadm]} && ${#UPDATED_FILES[@]} )); then
  yadm add "${UPDATED_FILES[@]}"

  print -u2 "\nDo you want to commit the following updated Bat themes to yadm?"

  for f in "${UPDATED_FILES[@]}"; do
    print -u2 "  ${f}"
  done

  print -u2 -n "> Commit these files? (y/N): "
  read -r reply

  if [[ "$reply" =~ ^[Yy] ]]; then
    yadm commit "${UPDATED_FILES[@]}" -m "chore(bat): Update themes"
  else
    print -u2 "Skipped yadm commit."
  fi
fi
